buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

repositories {
	maven { url = "http://mobiusstrip.eu/maven/" } // Waila
	maven { url = "http://chickenbones.net/maven/" } // CCC & NEI
	maven { url = "http://dvs1.progwml6.com/files/maven/" } // TCon and Mantle. Not a hard dep for the mod, but we are developing against it.

	ivy {
	name "MineTweaker3"
	artifactPattern "http://minetweaker3.powerofbytes.com/download/[module]-[revision].[ext]"
	}
}

dependencies {

	compile "tconstruct:TConstruct:1.7.10-1.8.2.build857:deobf"

	compile "mcp.mobius.waila:Waila:1.5.8a_1.7.10" //Primary purpose of WAILA is for easier testing in my dev env, hah.
	compile "codechicken:CodeChickenCore:1.7.10-1.0.4.29:dev"
	compile name: 'MineTweaker3', version: "Dev-1.7.10-3.0.9B", ext: 'jar'

	compile fileTree(dir: 'libs', include: '*.jar')
}

version = "0.0.1"
group= "ws.zettabyte.oldowanobsidian"
archivesBaseName = "oldowanObsidian"

minecraft {
    version = "1.7.10-10.13.2.1343-1.7.10"
    runDir = "eclipse"
}

processResources
{
    // Get a build number, increment it. Thank you 
    // http://stackoverflow.com/questions/21405457/autoincrement-versioncode-with-gradle-extra-properties
    // for this code, which I have adapted.
    def buildnum = 0
    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
    	def Properties versionProps = new Properties()
    	versionProps.load(new FileInputStream(versionPropsFile))

    	buildnum = versionProps['BUILD_NUMBER'].toInteger() + 1
    	versionProps['BUILD_NUMBER']=buildnum.toString()
    	versionProps.store(versionPropsFile.newWriter(), null)
    }
    else {
    	throw new GradleException("Could not read version.properties!")
    } 

    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version + "." + buildnum.toString()
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
